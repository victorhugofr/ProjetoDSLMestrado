/*
 * generated by Xtext 2.25.0
 */
package com.ufrn.atad.generator

import com.ufrn.atad.atad.Clicar
import com.ufrn.atad.atad.Escrever
import com.ufrn.atad.atad.Navegar
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import com.ufrn.atad.atad.VerifiquePresente
import com.ufrn.atad.atad.VerifiqueNaoPresente
import com.ufrn.atad.atad.EsperaPresente
import com.ufrn.atad.atad.EsperaVisivel
import com.ufrn.atad.atad.EsperaClicavel

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class AtadGenerator extends AbstractGenerator {

	 override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		var a="";
		a+=buildTestPre() 
        for (e : resource.allContents.toIterable) {
        	if(e instanceof Clicar)
        		a+=comandoClicar(e)
        	else if(e instanceof Navegar)
        	 	a+=comandoNavegar(e)
        	else if(e instanceof EsperaPresente)
        	 	a+=comandoEsperaPresente(e)
        	else if(e instanceof EsperaVisivel)
        	 	a+=comandoEsperaVisivel(e)
        	else if(e instanceof EsperaClicavel)
        	 	a+=comandoEsperaClicavel(e)
        	 else if(e instanceof VerifiquePresente)
        	 	a+=comandoVerifique(e)
        	 else if(e instanceof VerifiqueNaoPresente)
        	 	a+=comandoNaoVerifique(e)
        	  else if(e instanceof Escrever)
        	 	a+=comandoEscrever(e)
        }
        a+=buildTestPos()
        	fsa.generateFile("Teste.java", a)
    }
    
    private def buildTestPre()'''
    import org.junit.Test;
    import org.openqa.selenium.By;
    import static org.junit.Assert.assertTrue;
    import org.openqa.selenium.WebDriver;
    import org.openqa.selenium.support.ui.ExpectedConditions;
    import org.openqa.selenium.support.ui.WebDriverWait;
    import org.openqa.selenium.firefox.FirefoxDriver;
    
    public class Teste{
    	protected WebDriver driver = new FirefoxDriver();
    	protected WebDriverWait wait = new WebDriverWait(driver,30);
    		
    	@Test
    	public void teste() throws InterruptedException{
    		driver.manage().window().maximize();
   '''
   
   private def buildTestPos()'''
    	}
    }
   '''
    private def comandoEsperaPresente(EsperaPresente ep)'''
    		wait.until(ExpectedConditions.presenceOfElementLocated(By.«ep.tipoLocalizador»("«ep.name»")));
   '''
   
   private def comandoEsperaVisivel(EsperaVisivel ev)'''
    		wait.until(ExpectedConditions.visibilityOfElementLocated(By.«ev.tipoLocalizador»("«ev.name»")));
   '''
   
   private def comandoEsperaClicavel(EsperaClicavel ec)'''
    		wait.until(ExpectedConditions.elementToBeClickable(By.«ec.tipoLocalizador»("«ec.name»")));
   '''
   
    private def comandoClicar(Clicar c)'''
    		driver.findElement(By.«c.tipoLocalizador»("«c.name»")).click();
   '''
   
    private def comandoVerifique(VerifiquePresente v)'''
    		assertTrue(driver.getPageSource().contains("«v.name»"));
   '''
   
   private def comandoNaoVerifique(VerifiqueNaoPresente v)'''
    		assertTrue(!driver.getPageSource().contains("«v.name»"));
   '''
   
   private def comandoNavegar(Navegar n)'''
    		driver.get("«n.name»");
   '''
   
    private def comandoEscrever(Escrever e)'''
    		driver.findElement(By.«e.tipoLocalizador»("«e.name»")).sendKeys("«e.conteudo»");
   '''

}
